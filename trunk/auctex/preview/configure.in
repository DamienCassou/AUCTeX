AC_DIVERT_PUSH(AC_DIVERSION_INIT)dnl
[MY_LANG=$LANG
 MY_LC_CTYPE=$LC_CTYPE]
AC_DIVERT_POP()

AC_INIT(preview.el)
AC_SET_MAKE
AC_PROG_INSTALL



dnl
dnl Apparently, if you run a shell window in Emacs, it sets the EMACS
dnl environment variable to 't'.  Lets undo the damage.
dnl
if test "${EMACS}" = "t"; then
   EMACS=""
fi

AC_ARG_WITH(emacs,[  --with-emacs            Use Emacs to build], 
	[ if test "${withval}" = "yes"; then EMACS=emacs; else EMACS=${withval}; fi ])
AC_ARG_WITH(xemacs,[  --with-xemacs          Use XEmacs to build], 
	[ if test "${withval}" = "yes"; then EMACS=xemacs; else EMACS=${withval}; fi ])

AC_ARG_WITH(texmf,[  --with-texmf=DIR        TEXMF tree to install into],
 [ texmfdir=${withval} ; 
   if test ! -d $texmfdir  ; then
      AC_MSG_ERROR([--with-texmf=$texmfdir: Directory does not exist])
   fi
   previewtexmfdir=$texmfdir/tex/latex/preview
   previewdocdir=$texmfdir/doc/latex/styles
   ])

AC_ARG_WITH(preview-dir,[  --with-preview-dir=DIR        Location to install preview],
 [ previewtexmfdir=${withval} ; 
   if test ! -d $previewtexmfdir  ; then
      AC_MSG_ERROR([--with-preview-dir=$previewtexmfdir: Directory does not exist])
   fi
   ])


dnl FIXME we should check that it's the right version
AC_CHECK_PROGS_REQUIRED(EMACS, emacs xemacs, [(X)Emacs not found!  Aborting!  
You must install Emacs 21 or XEmacs for preview to work.])

AC_XEMACS_P

if test $EMACS_FLAVOR = "xemacs"  ; then
   PLAT_LISP=prv-xemacs.el
else		
   PLAT_LISP=prv-emacs.el
fi

AC_SUBST(PLAT_LISP)

AC_CHECK_AUCTEX

AC_PATH_PROG_REQUIRED(GS, gs, [gs not found!  Aborting!  
You must install Ghostscript for preview to work.])

dnl check to see if GS supports -dTextAlphaBits and -dGraphicsAlphaBits
dnl Check to see if specific drivers are built in.  Need table of mappings, with priority



AC_PATH_PROG_REQUIRED(LATEX, latex, [LaTeX not found, aborting!
You must install LaTeX for preview to work.])

AC_MSG_CHECKING([if LaTeX is installed 8-bit clean])
LANG=$MY_LANG
LC_CTYPE=$MY_LC_CTYPE
export LANG
export LC_CTYPE

if latex '\nonstopmode\message{^^fc!}' 2>&1 | tee latex.out | grep -q 'fc!$'
then
   AC_MSG_ERROR([
   LaTeX is not installed 8-bit clean.  Check the PROBLEMS file for details.])
else
   AC_MSG_RESULT(yes)
fi


AC_PATH_PROG_REQUIRED(TEX, tex, NONE)
AC_PATH_PROG_REQUIRED(TEXHASH, texhash, NONE)

AC_SUBST(auctexdir)

AC_PATH_TEXMFDIR
AC_PATH_LISPDIR
AC_OUTPUT(Makefile)
cat >&2 <<EOF

Congratulations!  Configuration is complete.

Type "make" at the prompt to build preview.
EOF
