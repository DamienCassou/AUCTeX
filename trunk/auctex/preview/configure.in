dnl
dnl Now we use the old AC_INIT macro for versions 2.0 to 2.19.  That
dnl should make the proper split between 2.13 and 2.49a.
dnl
ifelse(regexp(AC_ACVERSION,[^2\.[01][0-9]?[^0-9]*$]),-1,
  [AC_INIT(preview-latex,0.9,preview-latex-bugs@lists.sourceforge.net)],
  [AC_INIT(preview.el)])

AC_SET_MAKE
AC_PROG_INSTALL

if test "$prefix" = NONE
then
   prefix="$ac_prefix_default"
fi
if test "$exec_prefix" = NONE
then
   exec_prefix='$prefix'
fi

EMACS_PROG_EMACS


# The Debian package uses `--disable-build-dir-test'; normal users should
# never need to modify the default behavior.
AC_ARG_ENABLE([build-dir-test],
  [  --enable-build-dir-test test for build directory inside `load-path'
			  (on by default)],
  [if ! [[ "X-${enableval}" = "X-no" ]]
     then VALID_BUILD_DIR
   fi],
  [VALID_BUILD_DIR])

if test $EMACS_FLAVOR = "xemacs"  ; then
   PLAT_LISP=prv-xemacs.el
   EMACS_PATH_PACKAGEDIR([preview],[preview])
else
   PLAT_LISP=prv-emacs.el
   packagedir=no
fi

if test "$packagedir" = no
then
   EMACS_PATH_LISPDIR([preview-latex])
   icondir=preview
   packagelispdir=preview
else
   lispdir="$packagedir/lisp/preview"
   packagelispdir=.
   icondir=../../etc/preview
   infodir=../../info
fi

AC_MSG_CHECKING([where the bulk of the package lisp files go])
EMACS_LISP_RELATIVE(lispdir,packagelispdir,
	[the package lisp files])
AC_MSG_RESULT([[${packagelispdir}]])
AC_SUBST(packagelispdir)

AC_MSG_CHECKING([where the icon files go])
EMACS_LISP_RELATIVE(lispdir,icondir,
	[the icon files])
AC_MSG_RESULT([[${icondir}]])
AC_SUBST(icondir)

AC_MSG_CHECKING([where the info files go])
EMACS_LISP_RELATIVE(lispdir,infodir,
	[the info files])
AC_MSG_RESULT([[${infodir}]])
AC_SUBST(infodir)

AC_SUBST(PLAT_LISP)
AC_SUBST(packagedir)

EMACS_CHECK_AUCTEX
AC_SUBST(auctexdir)

AC_CHECK_PROGS_REQUIRED(GS, gs GSWIN32C.EXE, 
[GhostScript not found!  Aborting!  
You need GhostScript in your PATH for preview to work.])

EMACS_LISP([[lispGS]],[[(if (string= dir \"gs\") \"\" (backquote (defvar preview-gs-command (, dir))))]],-no-site-file,[[dir]],[["$GS"]])
AC_SUBST(lispGS)

dnl check to see if GS supports -dTextAlphaBits and -dGraphicsAlphaBits
dnl Check to see if specific drivers are built in.
dnl Need table of mappings, with priority

AC_PATH_PROG_REQUIRED(LATEX, latex, [LaTeX not found, aborting!
You must install LaTeX for preview to work.])

AC_PATH_PROG(PDFLATEX, pdflatex)

# Determine which latex-style components to install
# To match .sty, .def, .cfg, but _not_ .drv
TEXMFGEN=[`sed -n 's/^%<installer>.*file[{]\\([^}.]*\\.[sdc][tef][yfg]\\)[}].*/\\1/p' latex/preview.dtx`]
# Replace newlines with spaces.
TEXMFGEN=`echo $TEXMFGEN`
AC_SUBST(TEXMFGEN)

AC_PATH_PROG_REQUIRED(TEX, tex, NONE)

# Be very careful about assuming things about the available hash programs
# MikTeX comes with a braindead link to mktexlsr, apparently, so first
# try the regular utility from them.
AC_PATH_PROG(TEXHASH, texhash)
if test -z "$TEXHASH"; then
  AC_CHECK_PROG(TEXHASH, initexmf, initexmf -u)
  if test -z "$TEXHASH"; then
    AC_PATH_PROGS(TEXHASH, mktexlsr MakeTeXls-R, :)
  fi
fi

TEX_PATH_TEXMFDIR

AC_PATH_PROG(MAKEINFO, makeinfo, :)
MAKEINFO_CHECK_MACROS(acronym env option)
AC_PATH_PROG(INSTALL_INFO, install-info, :, $PATH /usr/sbin /sbin)

AC_SHELL_QUOTIFY(texmfdir)
AC_SHELL_QUOTIFY(prefix)
AC_SHELL_QUOTIFY(previewtexmfdir)
AC_SHELL_QUOTIFY(previewdocdir)
AC_SHELL_QUOTIFY(packagedir)
AC_SHELL_QUOTIFY(lispdir)
AC_SHELL_QUOTIFY(icondir)
AC_SHELL_QUOTIFY(infodir)
AC_SHELL_QUOTIFY(auctexdir)
AC_SHELL_QUOTIFY(datadir)
AC_SHELL_QUOTIFY(TEXHASH)
AC_SHELL_QUOTIFY(EMACS)
AC_SHELL_QUOTIFY(INSTALL_INFO)
AC_SHELL_QUOTIFY(MAKEINFO)
AC_SHELL_QUOTIFY(TEX)
AC_SHELL_QUOTIFY(LATEX)
AC_SHELL_QUOTIFY(PDFLATEX)

AC_OUTPUT(Makefile auto.el doc/Makefile latex/Makefile)
cat >&2 <<EOF

Congratulations!  Configuration is complete.

Type "make" at the prompt to build preview.
EOF
