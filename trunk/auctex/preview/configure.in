AC_INIT(preview.el)

AC_SET_MAKE
AC_PROG_INSTALL

dnl
dnl Apparently, if you run a shell window in Emacs, it sets the EMACS
dnl environment variable to 't'.  Lets undo the damage.
dnl
if test "${EMACS}" = "t"; then
   EMACS=""
fi

AC_ARG_WITH(emacs,[  --with-emacs            Use Emacs to build], 
	[ if test "${withval}" = "yes"; then EMACS=emacs; else EMACS=${withval}; fi ])
AC_ARG_WITH(xemacs,[  --with-xemacs          Use XEmacs to build], 
	[ if test "${withval}" = "yes"; then EMACS=xemacs; else EMACS=${withval}; fi ])

AC_ARG_WITH(auctex,[  --with-auctex=DIR       Location of AUC-TeX, if not standard], 
 [ auctexdir=${withval} ; 
   if test ! -d $auctexdir  ; then
      AC_MSG_ERROR([--with-auctex=$auctexdir: Directory does not exist])
   fi
])

AC_ARG_WITH(texmf,[  --with-texmf=DIR        TEXMF tree to install into],
 [ texmfdir=${withval} ; 
   if test ! -d $texmfdir  ; then
      AC_MSG_ERROR([--with-texmf=$texmfdir: Directory does not exist])
   fi
   previewtexmfdir=$texmfdir/preview])

AC_ARG_WITH(preview-dir,[  --with-preview-dir=DIR        Location to install preview],
 [ previewtexmfdir=${withval} ; 
   if test ! -d $previewtexmfdir  ; then
      AC_MSG_ERROR([--with-preview-dir=$previewtexmfdir: Directory does not exist])
   fi
   ])


dnl FIXME we should check that it's the right version
AC_PATH_PROG(EMACS, $EMACS, emacs)
AC_XEMACS_P

if test $EMACS_FLAVOR = "xemacs"  ; then
   PLAT_LISP=prv-xemacs.el
else		
   PLAT_LISP=prv-emacs.el
fi

AC_SUBST(PLAT_LISP)

AC_PATH_PROG(LATEX, latex, latex)
AC_PATH_PROG(TEX, tex, tex)
AC_PATH_PROG(TEXHASH, texhash, texhash)

AC_SUBST(auctexdir)

AC_PATH_TEXMFDIR
AC_PATH_LISPDIR
AC_OUTPUT(Makefile)
